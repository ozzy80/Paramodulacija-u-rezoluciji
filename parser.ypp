
%{
#include <cstdio>

int yylex();

#define yyerror printf

#include "fol.hpp"

Formula parsed_formula;
/*Ovde se smestaju funkcijski i predikatski simboli*/
Signature sig;

%}

%token<str_attr> SYMBOL;
%token<str_attr> VARIABLE;
%token TRUE;
%token FALSE;
%token NOT;
%token AND;
%token OR;
%token IMP;
%token IFF;
%token<str_attr> EQ;
%token<str_attr> NEQ;
%token FORALL;
%token EXISTS;

%type<formula_attr> formula
%type<formula_attr> iff_formula
%type<formula_attr> imp_formula
%type<formula_attr> or_formula
%type<formula_attr> and_formula
%type<formula_attr> unary_formula
%type<formula_attr> sub_formula
%type<formula_attr> atomic_formula
%type<formula_attr> atom
%type<term_attr> term
%type<term_seq_attr> term_seq

%union {
  string * str_attr;
  BaseFormula * formula_attr;
  BaseTerm * term_attr;
  vector<Term> * term_seq_attr;
}

%%

input  : formula ';'
       {
         parsed_formula = Formula($1);
       }
       ;

formula : iff_formula
	{
	 $$ = $1;
	}
	;

iff_formula : iff_formula IFF imp_formula
  	{
	  $$ = new Iff(Formula($1), Formula($3));
	}
	| imp_formula
	{
	  $$ = $1;
	}
	;

imp_formula : imp_formula IMP or_formula
	    {
	      $$ = new Imp(Formula($1), Formula($3));
	    }
	    | or_formula
	    {
	      $$ = $1;
	    }
	    ;

or_formula : or_formula OR and_formula
	   {
	     $$ = new Or(Formula($1), Formula($3));
	   }
	   | and_formula
	   {
	     $$ = $1;
	   }
	   ;

and_formula : and_formula AND unary_formula
	    {
	      $$ = new And(Formula($1), Formula($3));
	    }
	    | unary_formula
	    {
	      $$ = $1;
	    }
	    ;

unary_formula : NOT unary_formula
	      {
	       $$ = new Not(Formula($2));
	      }
	      | FORALL '[' VARIABLE ']' ':' unary_formula
	      {
	       $$ = new Forall(*$3, Formula($6));
	       delete $3;
	      }
	      | EXISTS '[' VARIABLE ']' ':' unary_formula
	      {
	       $$ = new Exists(*$3, Formula($6));
	       delete $3;
	      }
	      | sub_formula
	      {
	       $$ = $1;
	      }
	      ;

sub_formula : atomic_formula
	    {
	      $$ = $1;
	    } 
	    | '(' formula ')'
	    {
	      $$ = $2;
	    }
	    ;

atomic_formula : atom
	       {
	         $$ = $1;
	       }
	       | TRUE
	       {
	         $$ = new True();
	       }
	       | FALSE
	       {
	         $$ = new False();
	       }
	       ;

atom : VARIABLE
     {
       sig.addPredicateSymbol(*$1, 0);
       $$ = new Atom(sig, *$1);
       delete $1;
     }
     | VARIABLE '(' term_seq ')'
     {
       sig.addPredicateSymbol(*$1, (*$3).size());
       $$ = new Atom(sig, *$1, *$3);
       delete $1;
       delete $3;
     }
     | term EQ term
     {
       sig.addPredicateSymbol(*$2, 2);
       $$ = new Equality(sig, Term($1), Term($3));
       delete $2;
     }
     | term NEQ term
     {
       sig.addPredicateSymbol(*$2, 2);
       $$ = new Disequality(sig, Term($1), Term($3));
       delete $2;
     }
     ;

term_seq : term_seq ',' term
	 {
	   $$ = $1;
	   $$->push_back(Term($3));
	 }
	 | term
	 {
	   $$ = new vector<Term>();
	   $$->push_back(Term($1));
	 }
	 ;

term : VARIABLE
     {
       $$ = new VariableTerm(*$1);
       delete $1;
     }
     | SYMBOL
     {
       sig.addFunctionSymbol(*$1, 0);
       $$ = new FunctionTerm(sig, *$1);
       delete $1;
     }
     | SYMBOL '(' term_seq ')'
     {
       sig.addFunctionSymbol(*$1, (*$3).size());
       $$ = new FunctionTerm(sig, *$1, *$3);
       delete $1;
       delete $3;
     }
     ;

%%
